⍝ The sequence of triangle numbers is generated by adding the natural numbers.
⍝ So the 7th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first
⍝ ten terms would be:
⍝
⍝ 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
⍝
⍝ Let us list the factors of the first seven triangle numbers:
⍝
⍝  1: 1
⍝  3: 1,3
⍝  6: 1,2,3,6
⍝ 10: 1,2,5,10
⍝ 15: 1,3,5,15
⍝ 21: 1,3,7,21
⍝ 28: 1,2,4,7,14,28
⍝ We can see that 28 is the first triangle number to have over five divisors.
⍝
⍝ What is the value of the first triangle number to have over five hundred
⍝ divisors?

⍝ We'll express each number as a product of primes. Compute primes ≤⍵:
plte←{
    3 ⍵ 1{
        c s p←⍺
        c>s:⍵
        n←c+2
        n s(p+⍵[p]≤n*.5)∇⍵,(~0∊(p↑⍵)|c)/c
    }(0⌈1⌊⌊⍵-1)↑1⍴2
}

⍝ Compute a table of prime factor decompositions
dsieve←{(plte ⍵){(⍵,⍨⍴⍺)⍴∊⍺{∊+/⍵⍴¨1,⍨¨(¯1+⍺*⍳⌊⍺⍟⍵)⍴¨0}¨⍵}⍵}

⍝ Compute the number of factors
nfacs←{¯1↓{(1⌷⍵)××⌿1+1↓⍵}{⍵+1⌽⍵}dsieve⍵+1}

⍝ Keep checking larger N for numbers with more than ⍵ factors
fgt←{2 0{n a←⍺⋄a>0:a⋄⍵∇⍨(2×n),⊃⍸⍵<nfacs n}⍵}

⍝ Recover the actual triangular number
tfgt←{{2÷⍨⍵×⍵+1}fgt ⍵}

⍝ Calculate for 500
tfgt 500

⍝ As a one-liner
{{2÷⍨⍵×⍵+1}{2 0{n a←⍺⋄a>0:a⋄⍵∇⍨(2×n),⊃⍸⍵<{¯1↓{(1⌷⍵)××⌿1+1↓⍵}{⍵+1⌽⍵}{({3 ⍵ 1{c s p←⍺⋄c>s:⍵⋄n←c+2⋄n s(p+⍵[p]≤n*.5)∇⍵,(~0∊(p↑⍵)|c)/c}(0⌈1⌊⌊⍵-1)↑1⍴2} ⍵){(⍵,⍨⍴⍺)⍴∊⍺{∊+/⍵⍴¨1,⍨¨(¯1+⍺*⍳⌊⍺⍟⍵)⍴¨0}¨⍵}⍵}⍵+1} n}⍵}⍵}500

⍝ Indented
ti←{
    {
        2÷⍨⍵×⍵+1
    }{
        2 0{
            n a←⍺
            a>0:a
            ⍵∇⍨(2×n),⊃⍸⍵<{
                ¯1↓{
                    (1⌷⍵)××⌿1+1↓⍵
                }{
                    ⍵+1⌽⍵
                }{
                    ({
                        3 ⍵ 1{
                            c s p←⍺
                            c>s:⍵
                            n←c+2
                            n s(p+⍵[p]≤n*.5)∇⍵,(~0∊(p↑⍵)|c)/c
                        }(0⌈1⌊⌊⍵-1)↑1⍴2
                    }⍵){
                        (⍵,⍨⍴⍺)⍴∊⍺{
                            ∊+/⍵⍴¨1,⍨¨(¯1+⍺*⍳⌊⍺⍟⍵)⍴¨0
                        }¨⍵
                    }⍵
                }⍵+1
            } n
        }⍵
    }⍵
}
